##################################################################
# This macro exectutes DISCUS and KUPLOT via WWW interface.
#
# Version: 1.0
# Author:  Th. Proffen  (proffen@pa.msu.edu)
#          R.B. Neder   (reinhard.neder@mail.uni-wuerzburg.de)
##################################################################
# Required parameters:
#
#    $1  : Separation distance in A
#    $2  : Rotation angle around z-axis
#    $3  : Wavelength of X-rays
#    $4  : Output graphics size
#
##################################################################  
#
set error,exit
#
discus
#
read
free  1.0, 1.0, 1.0, 90.0, 90.0, 90.0
#
r[10]=$1*0.5
r[11]=$2
#
insert H,     r[10]*cosd(r[11]),     r[10]*sind(r[11]),0.0,0.1
insert H,-1.0*r[10]*cosd(r[11]),-1.0*r[10]*sind(r[11]),0.0,0.1
#
plot
ext all
sel H
set H,3,3,1.0
prog kuplot
outf tmp_$1_$2_$3_$4.plot
thick 1.0
run
exit
#
four
ll  -4.5, -4.5, 0.0
lr   4.5, -4.5, 0.0
ul  -4.5,  4.5, 0.0
na 301
no 301
xray
run
exit
#
output
form stan
outf tmp_$1_$2_$3_$4.inte
value inte
run
exit
exit
#
##################################################################
# Here we start KUPLOT
##################################################################
#
kuplot
rese
#
load cr,tmp_$1_$2_$3_$4.plot
load ni,tmp_$1_$2_$3_$4.inte
#
alloc circle,360
do i[1]=1,360
  x[3,i[1]]=1.0/$3 * (cosd(i[1])-1.0)
  y[3,i[1]]=1.0/$3 * (sind(i[1])    )
enddo
#
ksav 2
form sk,3
outf tmp_$1_$2_$3_$4.txy
run
#
ksav 2
form sx,0.0
outf tmp_$1_$2_$3_$4.hxy
run
#
load xy,tmp_$1_$2_$3_$4.txy
load xy,tmp_$1_$2_$3_$4.hxy
#
ccal mul,wx,4,0.5
#
alloc line,2
x[6,1]=xmin[2]
y[6,1]=0.0
x[6,2]=xmax[2]
y[6,2]=0.0
#
alloc line,2
y[7,1]=ymin[2]
x[7,1]=0.0
y[7,2]=ymax[2]
x[7,2]=0.0
#
buf 0.08
lwid 0,0.1
font just,center
fnam off
#
nfra 4
sfra 1,0.0,0.0,0.5,0.3
sfra 2,0.0,0.3,0.5,1.0
sfra 3,0.5,0.0,1.0,0.5
sfra 4,0.5,0.5,1.0,1.0
#
afra 1
buf 0.0
kfra 1,1
msiz 1,0.7
skal -($1+0.2),+($1+0.2),-($1+0.2),+($1+0.2)
aver 1.0
bond 1,$1,0.05,1,3,0.3
fset 0
sann 1,Real space,0.0,-($1+0.2),c
#
afra 2
kfra 2,2,3,6,7
hart 2,2
skal 
mark 2.0,2.0
fset 2
aver 1.0
cmap fire
lwid 3,0.7
lcol 3,9
lwid 6,0.7
lcol 6,8
lcol 7,8
hlin 1,0,125,1,%
cmap gray
achx h(\A\u-1\d)
achy k(\A\u-1\d)
achz Intensity
tit2 Reciprocal space
#
afra 3
kfra 3,4
lcol 4,9
skal xmin[4],xmax[4],0.0,1.1*ymax[4]
mark
tit2 \gH scan
achx \gH (degrees)
achy Intensity
#
afra 4
kfra 4,5
lcol 5,8
skal xmin[5],0.0,0.0,1.1*ymax[5]
mark
tit2 h scan
achx h (\A\u-1\d)
achy Intensity
#
save ps,teach_ewald_$1_$2_$3_$4.ps
size[3]=$4
save pic,teach_ewald_$1_$2_$3_$4.gif
exit
#
##################################################################
# Some cleaning up - only GIF/PS output remains
##################################################################
#
system rm -f tmp_$1_$2_$3_$4.*
exit
